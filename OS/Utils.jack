class Utils {
    static Array twoToThe;
    static boolean calledInit;
    static int ERR;

    function void init() {
        let ERR = 5;
        let calledInit = true;
        let twoToThe = Array.new(16);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        let twoToThe[15] = 16384 + 16384;   // shift to right
        return;
    }

    function int mod(int a, int b) {
        return a - ((a / b) * b);
    }

    function int twoToPow(int i) {
        // bootstraps itself :)
        if (~calledInit) { do Utils.init(); }

        if ((i < 0) | (i > 15)) { do Sys.error(ERR); }
        return twoToThe[i];
    }

    function String intToBin(int x) {
        var int i;
        var String bin;
        let bin = String.new(16);
        let i = 15;
        while (~(i < 0)) {
            if (Utils.bitIsSet(x, i)) {
                let bin = bin.appendChar(49); // '1'
            } else {
                let bin = bin.appendChar(48); // '0'
            }
            let i = i - 1;
        }
        return bin;
    }

    function int getIntegerAscii(int digit) {
        if ((digit < 0) | (digit > 9)) { do Sys.error(7); }
        // 48 is ascii code of '0'
        return 48 + digit;
    }

    function boolean isIntegerAscii(char code) {
        return ((code > 47) & (code < 58));
    }

    function int getIntegerFromAscii(char code) {
        if (~Utils.isIntegerAscii(code)) { do Sys.error(8); }
        return code - 48;
    }

    function String intToString(int x) {
        var int tmp, numDigits;
        var String xStr;

        // base case where it's already 0.
        if (x = 0) {
            let xStr = String.new(1);
            let xStr = xStr.appendChar(Utils.getIntegerAscii(0));
            return xStr;
        }
        if (x < 0) {
            let numDigits = 1;  // make space for minus sign too.
        }

        let tmp = x;
        while (~(tmp = 0)) {
            let numDigits = numDigits + 1;
            let tmp = tmp / 10;
        }
        let xStr = String.new(numDigits);
        do xStr.setInt(x);
        return xStr;
    }

    /* Returns true if ith bit of x is on. */
    function boolean bitIsSet(int x, int i) {
        return ~((Utils.twoToPow(i) & x) = 0);
    }

    /*
     * divideCeil(15, 4) = 4, not 3.
     * divideCeil(16, 4) = 4.
     */
    function int divideCeil(int x, int y) {
        var int q;
        let q = x / y;
        if ((q * y) = x) {
            return q;
        } else {
            return q + 1;
        }
    }
}
