// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 512 rows (indexed 0..511, top to bottom)
 * of 256 pixels each (indexed 0..255, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
    static boolean color;
    static int baseAddr;
    static int X, Y, word, wordsPerRow;
    static int ERR;
    static Array twoToThe;

    /** Initializes the Screen. */
    function void init() {
        let ERR = 4;
        let baseAddr = 16384;
        let X = 512; let Y = 256;
        let word = 16;
        let wordsPerRow = X / word;
        let color = true;

        let twoToThe = Array.new(16);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        let twoToThe[15] = 16384 + 16384;   // shift to right
        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int i, size;
        let size = wordsPerRow * Y;
        while (i < size) {
            do Screen.updateLocation(i, false);
            let i = i + 1;
        }
        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let color = b;
        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int addr, mask, div, mod;
        if (~(x < X) | (x < 0)) { do Sys.error(ERR); }
        if (~(y < Y) | (y < 0)) { do Sys.error(ERR); }

        let div = x / word;
        let mod = x - (div * word);
        let addr = (y * wordsPerRow) + div;
        let mask = twoToThe[mod];
        do Screen.updateLocation(addr, mask);
        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int dx, dy, diff, directionX, directionY;

        // simplified. it doesn't partially draw what's valid.
        if (~(x1 < X) | (x1 < 0)) { do Sys.error(ERR); }
        if (~(y1 < Y) | (y1 < 0)) { do Sys.error(ERR); }
        if (~(x2 < X) | (x2 < 0)) { do Sys.error(ERR); }
        if (~(y2 < Y) | (y2 < 0)) { do Sys.error(ERR); }

        // faster implementation for horizontal lines.
        if (y1 = y2) {
            do Screen.drawLineHorizontal(x1, x2, y1);
            return;
        }

        if (x1 < x2) { let directionX = 1; } else { let directionX = -1; }
        if (y1 < y2) { let directionY = 1; } else { let directionY = -1; }
        let dx = Math.abs(x1 - x2);
        let dy = Math.abs(y1 - y2);

        // Draws vertically first, since we might have a vertical
        // line to draw. Horizontal lines are handled in a faster way.
        // A vertical line will keep on drawing until its end if it starts ok.
        while ( ~(x1 = x2) | ~(y1 = y2) ) {
            do Screen.drawPixel(x1, y1);
            if (diff < 0) {
                let x1 = x1 + directionX;
                let diff = diff + dy;
            } else {
                let y1 = y1 + directionY;
                let diff = diff - dx;
            }
        }
        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int h, d, rSquared;
        let rSquared = r * r;
        let h = 0;
        while (~(h > r)) {
            let d = Math.sqrt(rSquared - (h * h));
            do Screen.drawSymetric(x-d, x+d, y, h);
            let h = h + 1;
        }
        return;
    }

    function void drawSymetric(int x1, int x2, int Yaxis, int dist) {
        do Screen.drawLineHorizontal(x1, x2, Yaxis + dist);
        do Screen.drawLineHorizontal(x1, x2, Yaxis - dist);
        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color.
     *
     * Copy-pasting from drawLineHorizontal so we compute the
     * constants that use division/multiplication only once,
     * and then keep on using addition. This should speed it up
     * significantly.
     */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int tmp, row, col;
        var int maskX1, maskX2, modX1, modX2, divX1, divX2;
        var int i, stop;

        // swap if needed to make x1 < x2, y1 < y2
        if (x1 > x2) { let tmp = x1; let x1 = x2; let x2 = tmp; }
        if (y1 > y2) { let tmp = y1; let y1 = y2; let y2 = tmp; }

        // Make x2 an exclusive interval - easier to work with.
        // So we draw in [x1, x2), from x1 to x2 without x2.
        let x2 = x2 + 1;

        let divX1 = x1 / word;
        let divX2 = x2 / word;
        let modX1 = x1 - (divX1 * word);
        let modX2 = x2 - (divX2 * word);

        // 0 0 0 ... 1  1 ... 1
        // ---------[x1 ... word-1]
        let maskX1 = ~(twoToThe[modX1] - 1);
        //  1  1  ...  1   0  ...  0
        // [word ... x2-1] x2  ...
        let maskX2 = twoToThe[modX2] - 1;

        let row = y1 * wordsPerRow;
        while (~(y1 > y2)) {
            let col = divX1;
            // x1 and x2 form a word or less; draw and exit
            if (divX1 = divX2) {
                do Screen.updateLocation(row + col, maskX1 & maskX2);
            } else {
                // draw from x1 to a word
                if (modX1 > 0) {
                    do Screen.updateLocation(row + divX1, maskX1);
                    let i = x1 + (word - modX1);
                    let col = col + 1;
                } else {
                    let i = x1;
                }
                // draws all words between [x1, x2), stopping at the last word before x2
                let stop = x2 - modX2;
                while (i < stop) {
                    // fill entire word with a 111...111 mask
                    do Screen.updateLocation(row + col, true);
                    // go to next word
                    let i = i + word;
                    let col = col + 1;
                }
                // draw from x2 to a previous word
                if (modX2 > 0) {
                    do Screen.updateLocation(row + col, maskX2);
                }
            }
            let row = row + wordsPerRow;
            let y1 = y1 + 1;
        }

        return;
    }


    /** Private functions. **/

    /**
     * The line to draw looks like schema below:
     *
     *  word----[x1---word---...----word---x2)
     *
     * So we can greatly increase speed if we draw the edges first:
     * - from x1 to a word (any number divisible by 16)
     * - from x2 to a previous word
     * - and fill in everything else in between with words, since
     *   filling with words is going to be very fast, not like pixel by pixel
     */
    function void drawLineHorizontal(int x1, int x2, int y) {
        var int tmp, row, col;
        var int maskX1, maskX2, modX1, modX2, divX1, divX2;
        var int i, stop;

        // swap if needed to make x1 < x2
        if (x1 > x2) { let tmp = x1; let x1 = x2; let x2 = tmp; }

        // Make x2 an exclusive interval - easier to work with.
        // So we draw in [x1, x2), from x1 to x2 without x2.
        let x2 = x2 + 1;

        let divX1 = x1 / word;
        let divX2 = x2 / word;
        let modX1 = x1 - (divX1 * word);
        let modX2 = x2 - (divX2 * word);

        // 0 0 0 ... 1  1 ... 1
        // ---------[x1 ... word-1]
        let maskX1 = ~(twoToThe[modX1] - 1);
        //  1  1  ...  1   0  ...  0
        // [word ... x2-1] x2  ...
        let maskX2 = twoToThe[modX2] - 1;

        let row = y * wordsPerRow;
        let col = divX1;
        // x1 and x2 form a word or less; draw and exit
        if (divX1 = divX2) {
            do Screen.updateLocation(row + col, maskX1 & maskX2);
            return;
        }
        // draw from x1 to a word
        if (modX1 > 0) {
            do Screen.updateLocation(row + divX1, maskX1);
            let i = x1 + (word - modX1);
            let col = col + 1;
        } else {
            let i = x1;
        }
        // draws all words between [x1, x2), stopping at the last word before x2
        let stop = x2 - modX2;
        while (i < stop) {
            // fill entire word with a 111...111 mask
            do Screen.updateLocation(row + col, true);
            // go to next word
            let i = i + word;
            let col = col + 1;
        }
        // draw from x2 to a previous word
        if (modX2 > 0) {
            do Screen.updateLocation(row + col, maskX2);
        }

        return;
    }

    /* Knows how to apply the mask for both colors. */
    function int updateLocation(int addr, int mask) {
        var int val;
        let addr = addr + baseAddr;
        let val = Memory.peek(addr);
        if (color) {
            let val = val | mask;
        } else {
            let val = val & ~mask;
        }
        do Memory.poke(addr, val);
        return;
    }

}
