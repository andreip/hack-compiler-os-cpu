// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array twoToThe;
    static int ERR;
    static int MINIMUM;

    /** Initializes the library. */
    function void init() {
        let ERR = 3;
        let twoToThe = Array.new(16);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        let twoToThe[15] = 16384 + 16384;   // shift to right
        let MINIMUM = twoToThe[15];
        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) { return -x; }
        return x;
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int i, prod, neg, partialY, tmp;

        if ((x = 0) | (y = 0)) { return 0; }

        let neg = ((x < 0) & (y > 0)) | ((x > 0) & (y < 0));
        let x = Math.abs(x);
        let y = Math.abs(y);

        // swap, to loop on the smaller number
        if (x < y) {
            let tmp = x;
            let x = y;
            let y = tmp;
        }

        // while i-th bit of y is 1, add the shifted x to the
        // final product.
        while (partialY < y) {
            if (~((twoToThe[i] & y) = 0)) {
                let prod = prod + x;
                let partialY = partialY + twoToThe[i];
            }
            let x = x + x;      // shift x to the right
            let i = i + 1;
        }

        if (neg) { return -prod; }
        return prod;
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var boolean neg;
        var int q;

        if (y = 0) { do Sys.error(ERR); } // division by 0

        // Edge case when x or y are the smallest possible number.
        if ((x = MINIMUM) & (y = MINIMUM)) { return 1; }
        if (y = MINIMUM) { return 0; }
        // Increase x by y so we can turn it positive and
        // reuse same algorithm below.
        if (x = MINIMUM) {
            let x = x + Math.abs(y);
            let q = 1;
        }

        let neg = ((x < 0) & (y > 0)) | ((x > 0) & (y < 0));
        let q = q + Math._dividePos(Math.abs(x), Math.abs(y));
        if (neg) { return -q; }
        return q;
    }

    /* Called internally, use divide() or '/' operand instead. */
    function int _dividePos(int x, int y) {
        var int q, reminder, tmp;

        if (y = 0) { do Sys.error(ERR); } // division by 0

        // The actual algorithm: if x / y = q1, reminder r1
        // and we know through recursivity that x / 2y = q2 r2
        // then x = y * q1 + r1 = 2 * y * q2 + r2
        // and need to figure out if r2 has a y in it or not
        // so final result is either (2 * q2) or (2 * q2 + 1)
        // also put y < 0 since y starts as positive and if it
        // is now negative, we overflowed.
        if ((y > x) | (y < 0)) { return 0; }
        let q = Math._dividePos(x, y + y);
        let tmp = y * q;
        let reminder = x - (tmp + tmp);  // x % (2y)
        // reminder is < 2y for sure; but how is it compared to y?
        if (reminder < y) {
            let q = q + q;
        } else {
            let q = (q + q) + 1;
        }

        return q;
    }

    /** Returns the integer part of the square root of x. */
    /* 0 <= x < 2^n, where n = 16 bits on hack architecture.
     * so we can go searching for sqrt(x) in the range
     * 0 <= sqrt(x) <= 2^(n/2) - 1.
     */
    function int sqrt(int x) {
        var int n, i, y, yy;  // y = 0
        let n = 16;
        let i = (n / 2) - 1;  // 2^(n/2) - 1 to start with
        while (~(i < 0)) {
            let yy = y + twoToThe[i];
            let yy = yy * yy;

            // if (y + 2^i)^2 <= x then we add 2^i to y
            // helping y converge to sqrt(x)
            if (~(yy > x) & (yy > 0)) {
                let y = y + twoToThe[i];
            }
            let i = i - 1;
        }
        return y;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a < b) { return b; }
        return a;
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a < b) { return a; }
        return b;
    }
}
